allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'jarmangit'

    sourceCompatibility = 1.8
    group = 'com.youdevise'
    if (System.getenv('BUILD_NUMBER') != null) {
        version = "0.0." + System.getenv('BUILD_NUMBER')
    }
}

ext {
    jacksonVersion = '2.7.4'
}

allprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

dependencies {
    compile "org.hamcrest:hamcrest-library:1.3"
    compile "com.google.guava:guava:21.0"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testCompile "junit:junit:4.12"
    testCompile "org.jmock:jmock:2.6.0-RC1"
}

allprojects {
    repositories {
        if (project.hasProperty('repoUrl')) {
            maven {
                name "nexus"
                url "${project.repoUrl}/groups/public"
            }
        } else {
            mavenCentral()
        }
    }
}

allprojects {
    apply plugin: 'maven'

    uploadArchives {
        if (project.hasProperty('repoUrl')) {
            repositories {
                mavenDeployer {
                    repository(url: "${project.repoUrl}/repositories/yd-release-candidates") {
                        authentication(userName: project.repoUsername, password: project.repoPassword)
                    }
                }
            }
        }
    }
}

buildscript {
    repositories {
        if (project.hasProperty('repoUrl')) {
            maven {
                url "${project.repoUrl}/groups/public"
            }
        }
        else {
            mavenCentral()
        }
    }

    dependencies {
        classpath "com.timgroup:gradle-jarmangit:1.0.+"
    }
}
