allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'jarmangit'

    sourceCompatibility = 1.8
    group = 'com.youdevise'
    if (System.getenv('BUILD_NUMBER') != null) {
        version = "0.0." + System.getenv('BUILD_NUMBER')
    }
}

ext {
    jacksonVersion = '2.8.7'
}

allprojects {
    jar {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': 'TIM Group Ltd'
            )
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.encoding = "UTF-8"
        options.incremental = true
        options.deprecation = true
        options.compilerArgs << "-parameters"
    }

    repositories {
        maven {
            name "nexus"
            url "${project.repoUrl}/groups/public"
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                artifactId project.ext.has('artifactId') ? project.ext.artifactId
                        : project == rootProject ? project.name
                        : rootProject.name + "-" + project.name
                from components.java
                artifact(sourcesJar) {
                    classifier "sources"
                }
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

dependencies {
    compile "org.hamcrest:hamcrest-library:1.3"
    compile "com.google.guava:guava:21.0"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testCompile "junit:junit:4.12"
    testCompile "org.jmock:jmock:2.6.0-RC1"
}

buildscript {
    repositories {
        if (project.hasProperty('repoUrl')) {
            maven {
                url "${project.repoUrl}/groups/public"
            }
        }
        else {
            mavenCentral()
        }
    }

    dependencies {
        classpath "com.timgroup:gradle-jarmangit:1.0.+"
    }
}
