plugins {
    id "com.timgroup.jarmangit" version "1.1.113" apply false
}

allprojects {
    apply plugin: 'com.timgroup.jarmangit'

    group = 'com.youdevise'
    if (System.getenv('BUILD_NUMBER') != null) {
        version = "0.0." + System.getenv('BUILD_NUMBER')
    }
}

ext {
    jacksonVersion = '2.8.7'
}

allprojects {
    apply plugin: "java-library"

    tasks.withType(Jar).configureEach {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': 'TIM Group Ltd'
            )
        }
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.encoding = "UTF-8"
        options.incremental = true
        options.deprecation = true
        options.compilerArgs << "-parameters"
        options.compilerArgs << "-Xlint:unchecked"
    }

    repositories {
        maven {
            url = "$repoUrl/groups/public"
        }
    }

    apply plugin: 'maven-publish'

    tasks.register("sourcesJar", Jar) {
        dependsOn("classes")
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.named("assemble").configure {
        dependsOn("sourcesJar")
    }

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    url "$repoUrl/repositories/yd-release-candidates"
                    credentials {
                        username = project.repoUsername
                        password = project.repoPassword
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    artifactId project.ext.has('artifactId') ? project.ext.artifactId
                            : project == rootProject ? project.name
                            : rootProject.name + "-" + project.name
                    from components.java
                    artifact(sourcesJar)
                }
            }
        }
    }
}

dependencies {
    api "org.hamcrest:hamcrest-library:1.3"
    api "com.google.guava:guava:27.0.1-jre"
    api "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testImplementation "junit:junit:4.12"
    testImplementation "org.jmock:jmock:2.6.0"
}
